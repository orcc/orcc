/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/
// Retranspose.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

// Put pixels out of col IDCT back into scan order

import all caltrop.lib.BitOps;

actor Retranspose ( int isz, int osz )
   int(size=isz) X0, int(size=isz) X1, int(size=isz) X2, int(size=isz) X3 ==> int(size=osz) Y :

  //List[ List[ List[ int(size=osz) ] ] ] mem :=
  list ( type:int(size=osz), size=128) mem := 
          [  0 : for k in Integers(1, 128) ];

  int rcount := 0;
  int ccount := 0;
  int select := 0;

  action X0:[a], X1:[b], X2:[c], X3:[d] ==>
  guard rcount < 64
  var
    int row = rshift( rcount, 3 ),
    int quad = bitand( rshift( rcount, 2), 1)
  do
    // { 0, 7, 3, 4 }
    // { 1, 6, 2, 5 }
    if  quad = 0 then
      mem[select*64+row*8+0] := a;
      mem[select*64+row*8+7] := b;
      mem[select*64+row*8+3] := c;
      mem[select*64+row*8+4] := d;
    else
      mem[select*64+row*8+1] := a;
      mem[select*64+row*8+6] := b;
      mem[select*64+row*8+2] := c;
      mem[select*64+row*8+5] := d;
    end
    rcount := rcount + 4;
  end

  action ==> Y:[ mem[i*64+row*8+col] ]
  guard ccount > 0
  var
    int col = rshift( 64-ccount, 3 ),
    int row = bitand( 64-ccount, 7 ),
    int i = bitxor(select,1)
  do
    ccount := ccount - 1;
  end

  action ==>
  guard
    ccount = 0 and rcount = 64
  do
    select := bitxor( select, 1 );
    ccount := 64;
    rcount := 0;
  end

 end