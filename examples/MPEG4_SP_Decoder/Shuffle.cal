/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/
// Shuffle.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

// Reorder pipeline data before final scaling

import all caltrop.lib.BitOps;

actor Shuffle( int sz )
    int(size=sz) X0, int(size=sz) X1, int(size=sz) X2, int(size=sz) X3 ==>
    int(size=sz) Y0, int(size=sz) Y1, int(size=sz) Y2, int(size=sz) Y3 :

  int(size=sz) x4;
  int(size=sz) x5;
  int(size=sz) x6h;
  int(size=sz) x7h;
  int(size=27) x6l;
  int(size=27) x7l;

  a0: action X0:[a], X1:[b], X2:[c], X3:[d] ==> Y0:[a], Y1:[b]
  do
    x4 := c;
    x5 := d;
  end

  a1: action X0:[x2], X1:[a], X2:[x3], X3:[b] ==> Y2:[x2], Y3:[x3]
  var
    // Split into lower 8 bits and the upper part
    int(size=sz-8) ah = rshift(a,8),
    int(size=sz-8) bh = rshift(b,8),
    int(size=9) al = bitand(a,255),
    int(size=9) bl = bitand(b,255)
  do
    x6h := 181*(ah+bh);
    x7h := 181*(ah-bh);
    x6l := 181*(al+bl) + 128;
    x7l := 181*(al-bl) + 128;
  end

  a2: action ==> Y0:[x4], Y1:[x5], Y2:[x6h + rshift(x6l,8)], Y3:[x7h + rshift(x7l,8)] end

  schedule fsm s0 :
    s0( a0 ) --> s1;
    s1( a1 ) --> s2;
    s2( a2 ) --> s0;
  end
  
end
