/**
 * This software module was originally developed by 
 * He-Yuan Lin (NCKU), 
 * in the course of development of the ISO/IEC 23002-4 (Video Tool Library) 
 * for reference purposes and its performance may not have been optimized. 
 * This software module is an implementation of one or more tools as 
 * specified by the ISO/IEC 23002-4.
 * 
 * ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
 * license to copy, distribute, and make derivative works of this software 
 * module or modifications thereof for use in implementations of the 
 * ISO/IEC 23002-4 in products that satisfy conformance criteria (if any). 
 * 
 * Those intending to use this software module in products are advised that 
 * its use may infringe existing patents. ISO/IEC have no liability for use 
 * of this software module or modifications thereof.
 * 
 * Copyright is not released for products that do not conform to audiovisual 
 * and image-coding related ITU Recommendations and/or ISO/IEC International 
 * Standards. 
 * 
 * Assurance that the originally developed software module can be used 
 * (1) in the ISO/IEC 23002-4 once the ISO/IEC 23002-4 has been adopted; and 
 * (2) to develop the ISO/IEC 23002-4: 
 *
 *
 * NCKU grants ISO/IEC all rights necessary to 
 * include the originally developed software module or modifications thereof 
 * in the ISO/IEC 23002-4 and to permit ISO/IEC to offer You a royalty-free, 
 * worldwide, non-exclusive, copyright license to copy, distribute, and make 
 * derivative works for use in implementations of the ISO/IEC 23002-4 in 
 * products that satisfy conformance criteria (if any), and to the extent 
 * that such originally developed software module or portions of it are 
 * included in the ISO/IEC 23002-4.  To the extent that NCKU
 * owns patent rights that would be required to make, use, or 
 * sell the originally developed software module or portions thereof 
 * included in the ISO/IEC 23002-4 in a conforming product, NCKU
 * Rennes will assure the ISO/IEC that it is willing to 
 * negotiate licenses under reasonable and non-discriminatory terms and 
 * conditions with applicants throughout the world.
 *
 * ISO/IEC gives You a free license to this software module or modifications 
 * thereof for the sole  purpose of developing the ISO/IEC 23002-4.
 * 
 * NCKU Rennes retains full right to modify and use the 
 * code for its own purpose, assign or donate the code to a third party and 
 * to inhibit third parties from using the code for products that do not 
 * conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
 * Standards. 
 * 
 * This copyright notice must be included in all copies or derivative works. 
 * Copyright (c) ISO/IEC 2006-2010.
 */
// Algo_Interp_SeparableSixTapQuarterPeAVC.cal
//
// Author: He-Yuan Lin
//
// Fractional luma sample interpolation for motion compensation of AVC

//Modified by Damien de Saint Jorre ; IETR/INSA of Rennes (France)

package MPEG.MPEG4.part10;
import MPEG.Common.Imp_Math.*;
import std.util.BitOps.*;

actor Algo_Interp_SeparableSixTapQuarterPelAVC ()
	int (size=16) Mv,
	uint(size=8)  RD,
	uint(size=6)  PartSZ
		==>
	uint(size=8)  Interp
		:

  int(size=32) x ;
  int(size=32) y ;
  int(size=32) o_x ;
  int(size=32) o_y ;
  int(size=32) xFrac;
  int(size=32) yFrac;
  int width;
  int height;
  int temp;
  int temp2;
  List(type: List(type:uint(size=8), size=21 ), size=21) TabPix;
  List( type:int(size=32), size=6 ) half_pel := [ 0 : for int i in 0 .. 5 ];

	function SixTapFilter(int a1, int b1, int c1, int c2, int b2, int a2)
				-->
			int
		:
			a1 - (5 * b1) + (20 * c1) + (20 * c2) - (5 * b2) + a2
		end

	function ComputeHalfPixel(int a1, int b1, int c1, int c2, int b2, int a2)
				-->
			int
		:
			clip(
				( SixTapFilter(a1,b1,c1,c2,b2,a2) + 16 ) >> 5,
				0,255
			)
		end

	function ComputeQuarterPixel(int HalfPixel1, int HalfPixel2)
				-->
			int
		:
			clip(
				(HalfPixel1 + HalfPixel2 + 1) >> 1,
				0,255
			)
		end

	procedure compensate(uint(size=5)x_idx, uint(size=5)y_idx)
		begin
			if xFrac = 0 and yFrac = 0 then 
				temp := TabPix [2 + x_idx][2 + y_idx];
			end    
 			if xFrac = 0  and yFrac = 2 then
 				temp := ComputeHalfPixel(TabPix[2 + x_idx][y_idx]     , TabPix[2 + x_idx][1 + y_idx] , TabPix[2 + x_idx][2 + y_idx] ,
				                         TabPix[2 + x_idx][3 + y_idx] , TabPix[2 + x_idx][4 + y_idx] , TabPix[2 + x_idx][5 + y_idx]);
			end
			if xFrac = 0  and yFrac = 1 then
				temp := ComputeHalfPixel(TabPix[2 + x_idx][y_idx]     , TabPix[2 + x_idx][1 + y_idx] , TabPix[2 + x_idx][2 + y_idx] ,
				                         TabPix[2 + x_idx][3 + y_idx] , TabPix[2 + x_idx][4 + y_idx] , TabPix[2 + x_idx][5 + y_idx]);
				temp := ComputeQuarterPixel(temp,TabPix[2 + x_idx][2 + y_idx]);
			end
			if xFrac = 0  and yFrac = 3 then
				temp := ComputeHalfPixel( TabPix[2 + x_idx][y_idx]     , TabPix[2 + x_idx][1 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                          TabPix[2 + x_idx][3 + y_idx] , TabPix[2 + x_idx][4 + y_idx] , TabPix[2 + x_idx][5 + y_idx] );
				temp := ComputeQuarterPixel(temp,TabPix[2 + x_idx][3 + y_idx]);
			end  

			if xFrac = 2  and yFrac = 0 then
				temp := ComputeHalfPixel( TabPix[x_idx][2 + y_idx]     , TabPix[1 + x_idx][2 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                          TabPix[3 + x_idx][2 + y_idx] , TabPix[4 + x_idx][2 + y_idx] , TabPix[5 + x_idx][2 + y_idx]);
			end
			if xFrac = 2  and yFrac = 2 then 
				foreach int n in 0 .. 5
				do
					half_pel[n] := SixTapFilter( TabPix[x_idx][n + y_idx]     , TabPix[1 + x_idx][n + y_idx] , TabPix[2 + x_idx][n + y_idx],
					                             TabPix[3 + x_idx][n + y_idx] , TabPix[4 + x_idx][n + y_idx] , TabPix[5 + x_idx][n + y_idx]);
				end
				temp := SixTapFilter(half_pel[0] , half_pel[1] , half_pel[2] , half_pel[3] , half_pel[4] , half_pel[5]);
				temp := (temp + 512)>> 10;
				temp := clip(temp , 0,255);
			end
			if xFrac = 2  and yFrac = 1 then 
				foreach int n in 0 .. 5
				do
					half_pel[n] := SixTapFilter( TabPix[x_idx][n + y_idx]     , TabPix[1 + x_idx][n + y_idx] , TabPix[2 + x_idx][n + y_idx] ,
					                             TabPix[3 + x_idx][n + y_idx] , TabPix[4 + x_idx][n + y_idx] , TabPix[5 + x_idx][n + y_idx]);
				end
				temp  := SixTapFilter( half_pel[0] , half_pel[1] , half_pel[2] , half_pel[3] , half_pel[4] , half_pel[5]);
				temp  := (temp + 512) >> 10;
				temp  := clip(temp , 0,255);
				temp2 := ComputeHalfPixel( TabPix[x_idx][2 + y_idx]     , TabPix[1 + x_idx][2 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                           TabPix[3 + x_idx][2 + y_idx] , TabPix[4 + x_idx][2 + y_idx] , TabPix[5 + x_idx][2 + y_idx]);
				temp := ComputeQuarterPixel(temp,temp2);
			end
			if xFrac = 2  and yFrac = 3 then 
				foreach int n in 0 .. 5
				do
					half_pel[n] := SixTapFilter( TabPix[x_idx][n + y_idx]     , TabPix[1 + x_idx][n + y_idx] , TabPix[2 + x_idx][n + y_idx],
					                             TabPix[3 + x_idx][n + y_idx] , TabPix[4 + x_idx][n + y_idx] , TabPix[5 + x_idx][n + y_idx]);
				end
				temp  := SixTapFilter( half_pel[0] , half_pel[1] , half_pel[2] , half_pel[3] , half_pel[4] , half_pel[5]);
				temp  := (temp + 512) >> 10;
				temp  := clip(temp , 0,255);
				temp2 := ComputeHalfPixel( TabPix[x_idx][3 + y_idx]     , TabPix[1 + x_idx][3 + y_idx] , TabPix[2 + x_idx][3 + y_idx] ,
				                           TabPix[3 + x_idx][3 + y_idx] , TabPix[4 + x_idx][3 + y_idx] , TabPix[5 + x_idx][3 + y_idx]);
				temp  := ComputeQuarterPixel(temp,temp2);
			end
			if xFrac = 1  and yFrac = 0 then
				temp := ComputeHalfPixel( TabPix[x_idx][2 + y_idx]     , TabPix[1 + x_idx][2 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                          TabPix[3 + x_idx][2 + y_idx] , TabPix[4 + x_idx][2 + y_idx] , TabPix[5 + x_idx][2 + y_idx]);
				temp := ComputeQuarterPixel(temp,TabPix[2 + x_idx][2 + y_idx]);
			end  
			if xFrac = 1  and yFrac = 2 then
				foreach int n in 0 .. 5
				do
					half_pel[n] := SixTapFilter( TabPix[x_idx][n + y_idx]     , TabPix[1 + x_idx][n + y_idx] , TabPix[2 + x_idx][n + y_idx],
					                             TabPix[3 + x_idx][n + y_idx] , TabPix[4 + x_idx][n + y_idx] , TabPix[5 + x_idx][n + y_idx]);
				end
				temp  := SixTapFilter( half_pel[0] , half_pel[1] , half_pel[2] , half_pel[3] , half_pel[4] , half_pel[5]);
				temp  := (temp + 512) >> 10;
				temp  := clip(temp , 0,255);
				temp2 := ComputeHalfPixel( TabPix[2 + x_idx][y_idx]     , TabPix[2 + x_idx][1 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                           TabPix[2 + x_idx][3 + y_idx] , TabPix[2 + x_idx][4 + y_idx] , TabPix[2 + x_idx][5 + y_idx]);
				temp  := ComputeQuarterPixel(temp,temp2);
			end
			if xFrac = 1  and yFrac = 1 then
				temp  := ComputeHalfPixel( TabPix[x_idx][2 + y_idx]     , TabPix[1 + x_idx][2 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                           TabPix[3 + x_idx][2 + y_idx] , TabPix[4 + x_idx][2 + y_idx] , TabPix[5 + x_idx][2 + y_idx]);
				temp2 := ComputeHalfPixel( TabPix[2 + x_idx][y_idx]     , TabPix[2 + x_idx][1 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                           TabPix[2 + x_idx][3 + y_idx] , TabPix[2 + x_idx][4 + y_idx] , TabPix[2 + x_idx][5 + y_idx]);
				temp  := ComputeQuarterPixel(temp,temp2);
			end
			if xFrac = 1  and yFrac = 3 then
				temp := ComputeHalfPixel( TabPix[x_idx][3 + y_idx]     , TabPix[1 + x_idx][3 + y_idx] , TabPix[2 + x_idx][3 + y_idx],
				                          TabPix[3 + x_idx][3 + y_idx] , TabPix[4 + x_idx][3 + y_idx] , TabPix[5 + x_idx][3 + y_idx]);
				temp2 := ComputeHalfPixel( TabPix[2 + x_idx][y_idx]     , TabPix[2 + x_idx][1 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                           TabPix[2 + x_idx][3 + y_idx] , TabPix[2 + x_idx][4 + y_idx] , TabPix[2 + x_idx][5 + y_idx]);
				temp  := ComputeQuarterPixel(temp,temp2);
			end
			if xFrac = 3  and yFrac = 0 then
				temp := ComputeHalfPixel( TabPix[x_idx][2 + y_idx]     , TabPix[1 + x_idx][2 + y_idx] , TabPix[2 + x_idx][2 + y_idx] ,
				                          TabPix[3 + x_idx][2 + y_idx] , TabPix[4 + x_idx][2 + y_idx] , TabPix[5 + x_idx][2 + y_idx]);
				temp := ComputeQuarterPixel(temp,TabPix[3 + x_idx][2 + y_idx]);
			end  
			if xFrac = 3  and yFrac = 2 then
				foreach int n in 0 .. 5
				do
					half_pel[n] := SixTapFilter( TabPix[x_idx][n + y_idx]     , TabPix[1 + x_idx][n + y_idx] , TabPix[2 + x_idx][n + y_idx],
					                             TabPix[3 + x_idx][n + y_idx] , TabPix[4 + x_idx][n + y_idx] , TabPix[5 + x_idx][n + y_idx]);
				end
				temp  := SixTapFilter( half_pel[0] , half_pel[1] , half_pel[2] , half_pel[3] , half_pel[4] , half_pel[5]);
				temp  := (temp+512) >> 10;
				temp  := clip(temp , 0,255);
				temp2 := ComputeHalfPixel( TabPix[3 + x_idx][y_idx]     , TabPix[3 + x_idx][1 + y_idx] , TabPix[3 + x_idx][2 + y_idx] ,
				                           TabPix[3 + x_idx][3 + y_idx] , TabPix[3 + x_idx][4 + y_idx] , TabPix[3 + x_idx][5 + y_idx]);
				temp  := ComputeQuarterPixel(temp,temp2);
			end
			if xFrac = 3  and yFrac = 1 then
				temp := ComputeHalfPixel( TabPix[x_idx][2 + y_idx]     , TabPix[1 + x_idx][2 + y_idx] , TabPix[2 + x_idx][2 + y_idx],
				                          TabPix[3 + x_idx][2 + y_idx] , TabPix[4 + x_idx][2 + y_idx] , TabPix[5 + x_idx][2 + y_idx]);
				temp2 := ComputeHalfPixel( TabPix[3 + x_idx][y_idx]     , TabPix[3 + x_idx][1 + y_idx] , TabPix[3 + x_idx][2 + y_idx],
				                           TabPix[3 + x_idx][3 + y_idx] , TabPix[3 + x_idx][4 + y_idx] , TabPix[3 + x_idx][5 + y_idx]);
				temp  := ComputeQuarterPixel(temp,temp2);
			end
			if xFrac = 3  and yFrac = 3 then
				temp  := ComputeHalfPixel( TabPix[x_idx][3 + y_idx]     , TabPix[1 + x_idx][3 + y_idx] , TabPix[2 + x_idx][3 + y_idx],
				                           TabPix[3 + x_idx][3 + y_idx] , TabPix[4 + x_idx][3 + y_idx] , TabPix[5 + x_idx][3 + y_idx]);
				temp2 := ComputeHalfPixel( TabPix[3 + x_idx][y_idx]     , TabPix[3 + x_idx][1 + y_idx] , TabPix[3 + x_idx][2 + y_idx] ,
				                           TabPix[3 + x_idx][3 + y_idx] , TabPix[3 + x_idx][4 + y_idx] , TabPix[3 + x_idx][5 + y_idx]);
				temp  := ComputeQuarterPixel(temp,temp2);
			end
		end

	GetMv: action Mv:[ Mv_x , Mv_y ] ==>
	do
		x := 0;
		y := 0;
		o_x := 0;
		o_y := 0;
		xFrac := Mv_x & 3;
		yFrac := Mv_y & 3;
	end

	GetSize: action PartSZ:[ PartWidth, PartHeight ] ==>
	do
		width  := PartWidth;
		height := PartHeight;
	end

	GetPixVal.Launch: action
			RD:[d]
				==>
		guard
			y < (height+5)
		do
			TabPix[x][y] := d;
			x := x + 1;
			if x >= (width+5) then
				x := 0;
				y := y + 1;
			end
		end

	GetPixVal.Done: action ==>
		guard
			y >= height +5
		end

	Interpolate.Launch: action ==> Interp:[ p ]
		guard
			o_y < height
		var 
			uint(size=8) p
		do
			compensate(o_x,o_y);
			p := temp;
			o_x := o_x + 1;
			if o_x >= width then
				o_x := 0;
				o_y := o_y + 1;
			end
		end

	Interpolate.Done: action ==>
		guard
			o_y = height
		end

  schedule fsm GetMv:
    GetMv       (GetMv              )--> GetSize;

    GetSize     (GetSize            )--> GetValue;

	GetValue    (GetPixVal.Launch   )--> GetValue;
	GetValue    (GetPixVal.Done     )--> Interpolate;

    Interpolate (Interpolate.Launch )--> Interpolate;
    Interpolate (Interpolate.Done   )--> GetMv;
  end
end