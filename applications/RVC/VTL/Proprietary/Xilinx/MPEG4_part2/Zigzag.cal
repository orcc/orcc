/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2006, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/
// Zigzag.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

package Proprietary.Xilinx.MPEG4_part2;

actor Zigzag () int(size=SAMPLE_SZ) AC, int(size=3) START, int(size=7) ADDR ==>
int(size=SAMPLE_SZ) OUT :

	int SAMPLE_SZ = 13;

	int BUF_SIZE = 128;

	int(size=8) count := 1;

	bool half := false;

	List(type: int(size=SAMPLE_SZ), size=BUF_SIZE) buf := [ 0 : for int k in 1 .. BUF_SIZE ];

	skip: action START:[ s ] ==>
	guard
		s < 0
	end

	start: action START:[ s ] ==>
	end

	done: action ==>
	guard
		count = 64
	do
		count := 1;
		half := not half;
	end

	function wa() --> int :
		(count & 63) | if half then 64 else 0 end
	end

	function ra(int addr) --> int :
		(addr & 63) | if half then 0 else 64 end
	end

	// If there is not a block in the buffer already, just read in the block
	read_only: action AC:[ ac ] ==>
	do
		buf[wa()] := ac;
		count := count + 1;
	end

	// If there is a block in the buffer already but no new arrival, just write it out
	write_only: action ADDR:[ addr ] ==> OUT:[ buf[ra(addr)] ]
	do
		count := count + 1;
	end

	read_write: action ADDR:[ addr ], AC:[ ac ] ==> OUT:[ buf[ra(addr)] ]
	do
		buf[wa()] := ac;
		count := count + 1;
	end

	schedule fsm empty :
		empty ( skip ) --> empty;
		empty ( start ) --> read;
		full ( skip ) --> drain;
		full ( start ) --> both;
		read ( read_only ) --> read;
		read ( done ) --> full;
		drain ( write_only ) --> drain;
		drain ( done ) --> empty;
		both ( read_write ) --> both;
		both ( done ) --> full;
	end

	priority
		skip > start;
		done > read_only;
		done > write_only;
		done > read_write;
	end

end