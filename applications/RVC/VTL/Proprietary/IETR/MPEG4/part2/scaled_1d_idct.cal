package Proprietary.IETR.MPEG4.part2;

actor scaled_1d_idct () int(size=IN_SZ) In ==> int(size=OUT_SZ) Out :

// Port size declaration
	int IN_SZ = 32;

	int OUT_SZ = 32;

	function pmul_1_0(int X) --> int
	var
		int(size=32) Temp1 = (X >> 3) - (X >> 7) :
		X - Temp1
	end

	function pmul_1_1(int X) --> int
	var
		int(size=32) Temp1 = (X >> 3) - (X >> 7),
		int(size=32) Temp2 = Temp1 - (X >> 11) :
		Temp1 + (Temp2 >> 1)
	end

	function pmul_2_0(int X) --> int
	var
		int(size=32) Temp1 = (X >> 9) - X :
		(Temp1 >> 2) - Temp1
	end

	function pmul_2_1(int X) --> int :
		(X >> 1)
	end

	function pmul_3_0(int X) --> int
	var
		int(size=32) Temp1 = X + (X >> 5),
		int(size=32) Temp2 = (Temp1 >> 2) :
		Temp2 + (X >> 4)
	end

	function pmul_3_1(int X) --> int
	var
		int(size=32) Temp1 = X + (X >> 5),
		int(size=32) Temp2 = (Temp1 >> 2) :
		Temp1 - Temp2
	end

	action In:[ x ] repeat 8 ==> Out:[ y ] repeat 8
	var
		int xa , int xb , List(type: int, size=8) y , int temp1 , int temp2 , int temp3 , int temp4
	do
		xa := x[1] + x[7];
		xb := x[1] - x[7];
		x[1] := xa + x[3];
		x[3] := xa - x[3];
		x[7] := xb + x[5];
		x[5] := xb - x[5];
		temp1 := pmul_1_0(x[3]);
		temp4 := pmul_1_1(x[3]);
		temp3 := pmul_1_0(x[5]);
		temp2 := pmul_1_1(x[5]);
		x[3] := temp1 - temp2;
		x[5] := temp3 + temp4;
		temp1 := pmul_2_0(x[1]);
		temp4 := pmul_2_1(x[1]);
		temp3 := pmul_2_0(x[7]);
		temp2 := pmul_2_1(x[7]);
		x[1] := temp1 + temp2;
		x[7] := temp3 - temp4;
		temp1 := pmul_3_0(x[2]);
		temp4 := pmul_3_1(x[2]);
		temp3 := pmul_3_0(x[6]);
		temp2 := pmul_3_1(x[6]);
		x[2] := temp1 - temp2;
		x[6] := temp3 + temp4;
		xa := x[0] + x[4];
		xb := x[0] - x[4];
		x[0] := xa + x[6];
		x[6] := xa - x[6];
		x[4] := xb + x[2];
		x[2] := xb - x[2];
		y := [ x[0] + x[1], x[4] + x[5], x[2] + x[3], x[6] + x[7], x[6] - x[7], x[2] - x[3], x[4] - x[5], x[0] - x
		[1] ];
	end

end


      
     
