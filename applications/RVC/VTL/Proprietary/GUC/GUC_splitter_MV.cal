// splitter_MV
//
// Author Christophe Lucarz (christophe.lucarz@epfl.ch)
//
// Description for each block a couple (x,y) is associated, the coordinates of the motion vector. The x coordinate arrives first, followed by the Y  coordinate. This splitter send the 8 first token for the Y component (4 block  2 tokens), then, the 9th and 10th token to the U component and the 11th and  12th token to the U component 

package Proprietary.GUC;

actor GUC_splitter_MV () int(size=MV_SZ) MV, bool MOT ==> int(size=MV_SZ) Y ,
int(size=MV_SZ) U , int(size=MV_SZ) V :

// Port size declaration
	int MV_SZ = 9;


	
	// Skip btype token
	skipbtype: action  MOT:[mot] ==>
	guard
		not mot
	end

	// split the vectors
	B1: action MV:[ mv ] repeat 2, MOT:[mot] ==> Y:[ mv[0], mv[1] ]
	end

	B2: action MV:[ mv ] repeat 2, MOT:[mot] ==> Y:[ mv[0], mv[1] ]
	end

	B3: action MV:[ mv ] repeat 2, MOT:[mot] ==> Y:[ mv[0], mv[1] ]
	end

	B4: action MV:[ mv ] repeat 2, MOT:[mot] ==> Y:[ mv[0], mv[1] ]
	end

	B5: action MV:[ mv ] repeat 2, MOT:[mot] ==> U:[ mv[0], mv[1] ]
	end

	B6: action MV:[ mv ] repeat 2, MOT:[mot] ==> V:[ mv[0], mv[1] ]
	end

	schedule fsm cmd :
		
		cmd ( B1 ) --> block2;
		cmd ( skipbtype ) --> block2;
		block2 ( B2 ) --> block3;
		block2 ( skipbtype ) --> block3;
		block3 ( B3 ) --> block4;
		block3 ( skipbtype ) --> block4;
		block4 ( B4 ) --> block5;
		block4 ( skipbtype ) --> block5;
		block5 ( B5 ) --> block6;
		block5 ( skipbtype ) --> block6;
		block6 ( B6 ) --> cmd;
		block6 ( skipbtype ) --> cmd;
	end

	priority
		skipbtype > B1;
		skipbtype > B2;
		skipbtype > B3;
		skipbtype > B4;
		skipbtype > B5;
		skipbtype > B6;
	end

end