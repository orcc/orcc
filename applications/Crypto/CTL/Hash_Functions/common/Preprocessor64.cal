/**
 * This FU is part of a CAL implementation of Secure Hash Algorithms (SHA-1, SHA-2) specified in FIPS-180-3.
 * It performs the following steps as part of the preprocessing operation of the algorithm: 
 * 1) Padding of the input message to be a multiple of 64 bytes (512 bits).
 * 2) Parsing of the input message into multiple message block each of size 64 bytes (512 bits). 
 * 
 * FU ID: Preprocessor64
 * Two input port:
 * 		1) uint(size=64) IP1_Size: the size of the input message
 * 		2) uint(size=8) IP2_M: the input message (as byte stream) 
 * Two output port:
 * 		1) uint(size=32) OP1_N: the number of input message blocks
 * 		2) uint(size=8) OP2_M: the message block (16 bytes each)
 *
 * Developer: Junaid Jameel Ahmad @ Uni-Konstanz 2010
 */

package Hash_Functions.common;

actor Preprocessor64() uint(size=64) IP1_Size, uint(size=8) IP2_M ==> uint(size=32) OP1_N, uint(size=8) OP2_M:
	// State variables:
	uint BLOCK_SIZE = 64; // Block size of one message in bytes.
	uint(size=64) messageSize := 0; // Size of the current message in bytes.
	
	int action_index := 1;
	int byteCounter := 0;
	uint numZeroBytes := 0;
	uint N;
	
	// Receive message size and calculates:
	// 1) numZeroBytes: Number of zeros bytes to be padded.
	// 2) N: Number of 64 byte (512 bits) message blocks including 
	//    the padding, 0x80 byte and message size.
	receiveSize:
	action IP1_Size:[mSize] ==> OP1_N:[N]
	guard action_index = 1
	var
		uint(size=64) temp
	do
		messageSize := mSize;
		temp := messageSize + 9;
		if (temp mod BLOCK_SIZE) = 0 then
			N := temp / BLOCK_SIZE;
			numZeroBytes := 0;
		else
			N := temp / BLOCK_SIZE + 1;
			numZeroBytes := N * BLOCK_SIZE - temp;
		end		
		action_index := 2;
	end
	
	// Receive and forward input message byte by byte.
	receiveAndForwardMessage:
	action IP2_M:[b] ==> OP2_M:[b]
	guard action_index = 2 and byteCounter < messageSize
	do
		byteCounter := byteCounter + 1;
		if byteCounter = messageSize then
			action_index := 3;		
		end
	end
	
	// Once input message is finished, forward byte 0x80 (the most significant bit as 1).
	forward80Byte:
	action ==> OP2_M:[0x80]
	guard action_index = 3
	do
		action_index := 4;
	end	 
	
	// Forward the number of zeros bytes to be padded to make the message blocks to be of 64 bytes.
	forwardZeroBytes:
	action ==> OP2_M:[0x00]
	guard action_index = 4 and numZeroBytes > 0
	do
		numZeroBytes := numZeroBytes - 1;
		action_index := 4;
	end

	// Forward the message size as last 2 dwords of the last message block.
	forwardSize:
	action ==> OP2_M:[b] repeat 8
	guard action_index = 4 and numZeroBytes = 0
	var
		List(type:uint(size=8), size=8) b := [0: for int i in 0 .. 7],
		uint(size=64) mSize := messageSize * 8 // Size in bits
	do
		foreach int i in 0 .. 7 do
			b[7-i] := mSize & 0xff;
			mSize := mSize >> 8;
		end
		action_index := 1;
	end
end
