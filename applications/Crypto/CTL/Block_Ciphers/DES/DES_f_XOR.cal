/**
 * This FU is part of a CAL implementation of DES (FIPS 46-3).
 * It XORs input block with round key inside DES cipher function 'f'.
 * 
 * FU ID: DES_f_XOR
 * Three input ports:
 * 	 1) uint(size=1) IP1_Keys: 16 round keys of 48 bits each (768 bits in total)
 * 	 2) uint(size=8) IP2_RN: round number
 * 	 3) uint(size=1) IP3_Text: input text (48 bits) 
 * One output port:
 * 	 1) uint(size=1) OP: output text (48 bits) 
 *
 * Developers: Junaid Jameel Ahmad, Shujun Li @ Uni-Konstanz 2010
 */

package Block_Ciphers.DES;

actor DES_f_XOR () uint(size=1) IP1_Keys, uint(size=1) IP2_Text, uint(size=8) IP3_RN ==> uint(size=1) OP: 

	// Flag controlling firings of different actions.
	bool keysReceived := false;
	
	// A list for storing round keys.
	List(type:uint(size=1), size=768) K; // 48 bits * 16 keys = 768 bits

	readKey:
	action IP1_Keys:[keys] repeat 768 ==>
	guard not keysReceived
	do
		// Copy received round keys to state variable K.
		foreach int i in 0 .. 767 do
			K[i] :=  keys[i];
		end
		keysReceived := true;
	end

	// Receive text and XOR it with the proper round key.
	xorRoundKey:
	action IP2_Text:[x] repeat 48, IP3_RN:[roundNumber]
		==> OP:[ [x[i] ^ K[roundNumber*48+i]: for int i in 0 .. 47] ] repeat 48
	guard keysReceived and roundNumber < 16 // and roundNumber >=0
	end

end
