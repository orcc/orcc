/**
 * This FU is part of a CAL Cryptographic Tool Library (CTL) Utilities.
 * It combines a sequence of byte/word/double-word tokens into a sequence of word/double-word/quad-word tokens.
 * 
 * FU ID: Smaller2Bigger
 * One FU parameter:
 * 	 1) bool IS_BIG_ENDIAN: output bits in big-endian? (true = big-endian, false = little-endian)
 * Two Constants:
 * Note: Constants are being used instead of FU parameters because FU parameters are currently not fully 
 *       supported by Orcc. These constants will become FU parameters later.
 *	 1) int IP_PORT_SIZE: input port size (default: 8; possible values: 8, 16, 32)
 *	 2) int OP_PORT_SIZE: output port size (default: 32; possible values: 16, 32, 64)
 *	 Note: The value of IN_NUM_TOKENS (the number of input tokens to be used to generate one output token) 
 *          is calculated by IN_NUM_TOKENS := OP_PORT_SIZE / IP_PORT_SIZE.		
 * One input port:
 * 	 1) uint(size=IP_PORT_SIZE) IP: the input
 * One output port:
 * 	 1) uint(size=OP_PORT_SIZE) OP: the output token
 *
 * Developers: Junaid Jameel Ahmad, Shujun Li @ Uni-Konstanz 2010
 */

package Utilities;

actor Smaller2Bigger (bool IS_BIG_ENDIAN) uint(size=IP_PORT_SIZE) IP ==> uint(size=OP_PORT_SIZE) OP:
	
	int IP_PORT_SIZE = 8;
	int OP_PORT_SIZE = 32;
	
	// the number of input tokens to be used to generate one output token.
	int IN_NUM_TOKENS = OP_PORT_SIZE / IP_PORT_SIZE;
	int IN_NUM_TOKENS_1 = IN_NUM_TOKENS - 1;
		
	smaller2Bigger:
	action IP:[tokens] repeat IN_NUM_TOKENS ==> OP:[v]
	guard IN_NUM_TOKENS > 0
	var
		uint(size=OP_PORT_SIZE) v := 0
	do
		if IS_BIG_ENDIAN then
			foreach int i in 0 .. IN_NUM_TOKENS_1 do
				v := (v << IP_PORT_SIZE) | tokens[i];
			end		
		else
			foreach int i in 0 .. IN_NUM_TOKENS_1 do
				v := v | (tokens[i] << (i*IP_PORT_SIZE));
			end
		end	
	end
end
